plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
}

allprojects {
    repositories {
        maven {
            url = 'https://jitpack.io'
        }
        mavenCentral()
    }
}

android {
    namespace = 'com.legs.appsforaa'
    
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    packaging {
        resources {
            excludes += [
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/license.txt',
                'META-INF/NOTICE',
                'META-INF/NOTICE.txt',
                'META-INF/notice.txt',
                'META-INF/ASL2.0',
                'META-INF/*.kotlin_module',
                'META-INF/versions/9/OSGI-INF/MANIFEST.MF'  // BouncyCastle 1.78 conflict fix
            ]
        }
    }

    def localProperties = new Properties()
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    lint {
        // Disable translation-related errors (app falls back to English)
        disable 'MissingTranslation', 'ImpliedQuantity'
        abortOnError false
        checkReleaseBuilds true
    }

    signingConfigs {
        nuova {
            storeFile file(localProperties['STORELOCATION'])
            storePassword localProperties['STOREPASSWORD']
            keyAlias 'key3'
            keyPassword localProperties['KEYPASSWORD']
        }
    }





    compileSdk = 36

    defaultConfig {

        localProperties.load(new FileInputStream(rootProject.file("local.properties")))

        applicationId = "sksa.aa.customapps"
        minSdk = 24  // Increased for Shizuku support (Android 7.0+)
        targetSdk = 36
        versionCode = 18
        versionName = "2.1"
        signingConfig = signingConfigs.nuova
        base.archivesName = "AAAD-" + versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "STRIPE_PUBLISHABLE_KEY", "\"${localProperties['STRIPE_PUBLISHABLE_KEY']}\""
        buildConfigField "String", "STRIPE_PRICE_ID", "\"${localProperties['STRIPE_PRICE_ID']}\""
        buildConfigField "String", "STRIPE_PRICE_ID_PROMO", "\"${localProperties['STRIPE_PRICE_ID_PROMO']}\""
        buildConfigField "String", "FIREBASE_INSTANCE", "\"${localProperties['FIREBASE_INSTANCE']}\""
        buildConfigField "String", "FIREBASE_PROJECT_ID", "\"${localProperties['FIREBASE_PROJECT_ID']}\""
        buildConfigField "String", "FIREBASE_REGION", "\"${localProperties['FIREBASE_REGION']}\""
        buildConfigField "String", "AAPASSENGER_LINK", "\"${localProperties['AAPASSENGER_LINK']}\""
        buildConfigField "String", "CARSTREAM205_LINK", "\"${localProperties['CARSTREAM205_LINK']}\""
        buildConfigField "String", "CARSTREAM204_LINK", "\"${localProperties['CARSTREAM204_LINK']}\""
        buildConfigField "String", "CARSTREAM202_LINK", "\"${localProperties['CARSTREAM202_LINK']}\""
        buildConfigField "String", "AAMP_LINK", "\"${localProperties['AAMP_LINK']}\""
        buildConfigField "String", "AAMIRROR_LINK", "\"${localProperties['AAMIRROR_LINK']}\""
        buildConfigField "String", "AAWIDGETS_LINK", "\"${localProperties['AAWIDGETS_LINK']}\""
        buildConfigField "String", "AASTREAM_LINK", "\"${localProperties['AASTREAM_LINK']}\""
        buildConfigField "String", "APP_CATALOG_URL", "\"${localProperties['APP_CATALOG_URL']}\""

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // AndroidX Core Libraries (Updated to latest)
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.core:core-ktx:1.17.0'

    // Firebase - Using BoM for version management (Updated to 34.6.0)
    implementation platform('com.google.firebase:firebase-bom:34.6.0')
    implementation 'com.google.firebase:firebase-database'
    implementation 'com.google.firebase:firebase-storage'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-functions'

    // Networking and HTTP
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'commons-net:commons-net:3.12.0'
    implementation 'commons-io:commons-io:2.21.0'

    // Payment Processing (Updated to 22.2.0)
    implementation 'com.stripe:stripe-android:22.2.0'

    // HTML Parsing
    implementation 'org.jsoup:jsoup:1.21.2'

    // Kotlin Standard Library
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.2.21"

    // UI Components and Utilities
    implementation 'com.github.iGio90:BottomDialogs:master-SNAPSHOT'
    implementation 'com.github.blikoon:QRCodeScanner:0.1.2'

    // RecyclerView and UI components
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Image loading and caching
    implementation 'com.github.bumptech.glide:glide:5.0.5'

    // WorkManager for background tasks
    implementation 'androidx.work:work-runtime:2.11.0'
    implementation 'androidx.work:work-runtime-ktx:2.11.0'

    // Room for local caching
    implementation 'androidx.room:room-runtime:2.8.4'
    implementation 'androidx.room:room-ktx:2.8.3'
    kapt 'androidx.room:room-compiler:2.8.4'

    // Preferences DataStore
    implementation 'androidx.datastore:datastore-preferences:1.2.0'

    // Shizuku for Android 14+ silent installation
    def shizuku_version = "13.1.5"
    implementation "dev.rikka.shizuku:api:$shizuku_version"
    implementation "dev.rikka.shizuku:provider:$shizuku_version"

    // BouncyCastle for APK signing and certificate generation
    configurations.all {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15to18'
    }
    implementation('org.bouncycastle:bcpkix-jdk18on:1.82')
    implementation('org.bouncycastle:bcprov-jdk18on:1.82')

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}